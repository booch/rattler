# JSON parser based on the grammar at http://www.json.org

require 'rattler'
require_relative 'json_helper'

parser JsonParser < Rattler::Runtime::PackratParser

include JsonHelper

%whitespace (SPACE+ / comment)*

json_text <-  (object / array) EOF

object    <-  ~'{' members ~'}'                                     { object _ }

members   <-  pair *, ','

pair      <-  string ~':' value

array     <-  ~'[' elements ~']'                                    { array _ }

elements  <-  value *, ','

value     <-  object
            / array
            / number
            / string
            / `true`                                                { :true }
            / `false`                                               { :false }
            / `null`                                                { :null }
            / fail "value expected"

string    <-  @('"' char* '"')                                      { string _ }

number    <-  @(int frac? exp?)                                     { number _ }

%fragments

char      <-  !('"' / '\\' / CNTRL) .
            / '\\' (["\\/bfnrt] / 'u' XDIGIT XDIGIT XDIGIT XDIGIT)

int       <-  '-'? ('0' !DIGIT / [1-9] DIGIT*)

frac      <-  '.' DIGIT+

exp       <-  [eE] [+-]? DIGIT+

comment   <-  '/*' (! '*/' .)* '*/'
            / '//' [^\n]*

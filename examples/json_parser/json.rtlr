# JSON parser based on the grammar at http://www.json.org

require File.expand_path('json_helper', File.dirname(__FILE__))

parser JsonParser < Rattler::Runtime::PackratParser

include JsonHelper

%whitespace (SPACE+ | comment)* {

  object    <-  ~'{' members ~'}'                                   { object _ }

  members   <-  pair *, ','

  pair      <-  string ~':' value

  array     <-  ~'[' elements ~']'

  elements  <-  value *, ','

  value     <-  string
              | number
              | object
              | array
              | `true`                                              { :true }
              | `false`                                             { :false }
              | `null`                                              { :null }
              | fail "value expected"

  string    <-  @('"' char* '"')                                    { string _ }

  number    <-  @(int frac? exp?)                                   { number _ }
}

%inline {

  char      <-  !('"' | '\\' | CNTRL) .
              | '\\' (["\\/bfnrt] | 'u' XDIGIT XDIGIT XDIGIT XDIGIT)

  int       <-  '-'? ('0' !DIGIT | [1-9] DIGIT*)

  frac      <-  '.' DIGIT+

  exp       <-  [eE] [+-]? DIGIT+

  comment   <-  '/*' (! '*/' .)* '*/'
              | '//' [^\n]*
}

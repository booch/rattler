# JSON parser based on the grammar at http://www.json.org

parser JsonParser < Rattler::Runtime::PackratParser

include JsonHelper

%whitespace (SPACE+ | '/*' (! '*/' .)* '*/' | '//' [^\n]*)*

object    <-  ~'{' members ~'}'                                 { object _ }

members   <-  pair *^ ','

pair      <-  string ~':' value

array     <-  ~'[' elements ~']'

elements  <-  value *^ ','

value     <-  string                                            { string _ }
            | number !DIGIT                                     { _.to_f }
            | object
            | array
            | `true`                                            { :true }
            | `false`                                           { :false }
            | `null`                                            { :null }
            | fail "value expected"

string    <-  @('"' ('\\' . | [^"])* '"')

number    <-  @('-'? ('0' | [1-9] DIGIT*) ('.' DIGIT+)? ([eE] [+-]? DIGIT+)?)
